import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.application'
}

android {
    compileSdk = 35
    buildToolsVersion = '35.0.1'
    ndkVersion = '28.0.12674087'
    namespace = 'io.github.vvb2060.ndk.boringssl.test'
    defaultConfig {
        minSdk = 21
        targetSdk = 35
        versionCode = 20250114
        versionName = '20250114'
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            ndkBuild {
                arguments += [ "-j${Runtime.runtime.availableProcessors()}" ]
            }
        }
        ndk {
            debugSymbolLevel = "FULL"
            abiFilters += [ "x86", "x86_64", "armeabi-v7a", "arm64-v8a", "riscv64" ]
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            signingConfig = signingConfigs.debug
            proguardFiles("proguard-rules.pro")
            externalNativeBuild {
                ndkBuild {
                    arguments += [ "enableLTO=1" ]
                }
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path file('../boringssl/src/main/native/Android.mk')
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig = false
        prefab = true
    }

    lint {
        checkReleaseBuilds = false
    }

    dependenciesInfo {
        includeInApk = false
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    testOptions {
        devices {
            api30(ManagedVirtualDevice) {
                device = "Nexus One"
                apiLevel = 30
                systemImageSource = "aosp-atd"
                require64Bit = true
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
}
